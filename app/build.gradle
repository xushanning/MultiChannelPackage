apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"

    defaultConfig {
//        applicationId "com.xu.channel"
        minSdkVersion 19
        targetSdkVersion 29
//        versionCode 1
//        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    //多渠道打包
    flavorDimensions "default"
    productFlavors {
        //渠道包
        huawei {
            applicationId "com.xu.huawei"
            versionCode 1
            versionName "1.0"
            buildConfigField("String", "CHANNEL", "\"huawei\"")
            manifestPlaceholders = [app_name: "华为渠道app", key: "华为key", app_icon: "@mipmap/huawei_icon"]
        }
        xiaomi {
            applicationId "com.xu.xiaomi"
            versionCode 2
            versionName "2.0"
            buildConfigField("String", "CHANNEL", "\"xiaomi\"")
            manifestPlaceholders = [app_name: "小米渠道app", key: "小米key", app_icon: "@mipmap/xiaomi_icon"]
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == 'release') {
                outputFileName = "${variant.productFlavors[0].name}_${versionCode}_V${versionName}_" + releaseTime() + ".apk"
            }
        }
    }

}

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    //共同依赖lib
    implementation project(path: ':common_lib')
    //华为渠道依赖
    huaweiImplementation project(path: ':module_huawei')
    //小米渠道依赖
    xiaomiImplementation project(path: ':module_xiaomi')


}
